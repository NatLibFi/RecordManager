query=PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX wgs84: <http://www.w3.org/2003/01/geo/wgs84_pos#>
SELECT
    (?uri AS ?_id)
    ('finto' as ?type)
    (GROUP_CONCAT(DISTINCT STR(?prefLabel); separator='|') AS ?prefLabels)
    (GROUP_CONCAT(DISTINCT STR(?altLabel); separator='|') AS ?altLabels)
    (GROUP_CONCAT(DISTINCT STR(?hiddenLabel); separator='|') AS ?hiddenLabels)
    (CONCAT('POINT(', SAMPLE(?long), ' ', SAMPLE(?lat), ')') AS ?geoLocation)
WHERE {
  VALUES ?graph {
    <http://www.yso.fi/onto/yso/>
  }
  GRAPH ?graph {
    ?uri a skos:Concept .
    FILTER NOT EXISTS { ?uri owl:deprecated true }
    ?uri skos:prefLabel ?prefLabel .
    OPTIONAL {
      ?uri skos:altLabel ?altLabel
    }
    OPTIONAL {
      ?uri skos:hiddenLabel ?hiddenLabel
    }
    OPTIONAL {
      ?uri wgs84:lat ?lat .
      ?uri wgs84:long ?long .
    }
  }
}
GROUP BY ?uri
